{ inputs, lib, config, ... }:
let
  fp = inputs.flake-parts.lib;
  providerEnabled = config.provider.enable or false;
in
{
  options.perSystem = fp.mkPerSystemOption ({ config, pkgs, ... }:
    let
      ghcPackageNames = lib.unique config.hostenv.haskell.devPackages;
      devGhc = pkgs.haskellPackages.ghcWithPackages (p: map (name: p.${name}) ghcPackageNames);
      cabalHook = ''
        if command -v ghc >/dev/null; then
          libdir="$(ghc --print-libdir)"
          pkgdb="''${libdir}/package.conf.d"
          cat > "$PWD/cabal.project.local" <<EOF
        -- Generated by nix develop (flake-parts/devshells.nix)
        package-dbs: clear, ''${pkgdb}
        with-compiler: ghc
        EOF
        fi
      '';
    in
    {
      options.hostenv.haskell.devPackages = lib.mkOption {
        type = lib.types.listOf lib.types.str;
        default = [ ];
        description = "Haskell package names contributed by micro-projects for the default dev shell GHC environment.";
      };

      config = lib.mkIf (!(inputs ? hostenv)) {
        hostenv.haskell.devPackages =
          [ "haskell-language-server" ]
          ++ (lib.attrByPath [ "provider" "haskellDevPackages" ] [ ] config);
        devshells.default = {
          devshell = {
            packages =
              (lib.optional providerEnabled config.packages.hostenv-provider)
              ++ [
                pkgs.jq
                pkgs.sops
                pkgs.age
                pkgs.haskellPackages.cabal-install
                devGhc
              ];
            startup.cabal-project = { text = cabalHook; };
          };
          env = [
            {
              name = "DEVSHELL_NO_MOTD";
              value = 1;
            }
          ];
        };
      };
    });
}
