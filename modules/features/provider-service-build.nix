{ inputs, lib, config, ... }:
let
  fp = inputs.flake-parts.lib;
  cfgTop = config;
  providerEnabled = config.provider.enable or false;
in
{
  options.perSystem = fp.mkPerSystemOption ({ config, pkgs, ... }:
    let
      providerService = cfgTop.flake.lib.provider.service;
      packageNames = providerService.haskellDeps;
      serviceSrc = providerService.src;
      ghc = pkgs.haskellPackages.ghcWithPackages (p: map (name: p.${name}) packageNames);
      servicePkg = pkgs.writeShellScriptBin "hostenv-provider-service" ''
        exec ${ghc}/bin/runghc -i${serviceSrc} ${serviceSrc}/Main.hs "$@"
      '';

      # Quick hacky way to try out the provider service using a browser on local.
      servicePkgDev = pkgs.writeShellScriptBin "hostenv-provider-service-dev" ''
        set -euo pipefail

        base="''${HOSTENV_PROVIDER_DEV_DIR:-/tmp/hostenv-provider-dev}"
        pgdata="$base/pgdata"
        mkdir -p "$base"

        if [ ! -s "$pgdata/PG_VERSION" ]; then
          initdb -D "$pgdata" -A trust >/dev/null
        fi

        pg_ctl -D "$pgdata" -o "-k $base" -l "$base/postgres.log" start >/dev/null
        cleanup() {
          if [ -n "''${socat_pid:-}" ]; then
            kill "$socat_pid" >/dev/null 2>&1 || true
          fi
          pg_ctl -D "$pgdata" stop >/dev/null || true
        }
        trap cleanup EXIT

        createdb -h "$base" hostenv-provider >/dev/null 2>&1 || true

        secrets="''${HOSTENV_PROVIDER_GITLAB_SECRETS_FILE:-$base/gitlab_oauth}"
        if [ ! -f "$secrets" ]; then
          printf "client_id=dev\nclient_secret=dev\n" > "$secrets"
        fi

        export HOSTENV_PROVIDER_GITLAB_SECRETS_FILE="$secrets"
        export HOSTENV_PROVIDER_REPO_SOURCE="''${HOSTENV_PROVIDER_REPO_SOURCE:-$PWD}"
        export HOSTENV_PROVIDER_DATA_DIR="''${HOSTENV_PROVIDER_DATA_DIR:-$base/data}"
        export HOSTENV_PROVIDER_LISTEN_SOCKET="''${HOSTENV_PROVIDER_LISTEN_SOCKET:-$base/hostenv-provider.sock}"
        export HOSTENV_PROVIDER_WEBHOOK_HOST="''${HOSTENV_PROVIDER_WEBHOOK_HOST:-localhost}"
        export HOSTENV_PROVIDER_UI_BASE_URL="''${HOSTENV_PROVIDER_UI_BASE_URL:-http://localhost}"
        export HOSTENV_PROVIDER_DB_URI="''${HOSTENV_PROVIDER_DB_URI:-host=$base dbname=hostenv-provider}"

        if [ -n "''${HOSTENV_PROVIDER_HTTP_PORT:-}" ]; then
          socat TCP-LISTEN:"$HOSTENV_PROVIDER_HTTP_PORT",fork,reuseaddr UNIX-CONNECT:"$HOSTENV_PROVIDER_LISTEN_SOCKET" &
          socat_pid=$!
          echo "hostenv-provider-service-dev: proxying http://localhost:$HOSTENV_PROVIDER_HTTP_PORT -> unix:$HOSTENV_PROVIDER_LISTEN_SOCKET" >&2
        fi

        ${servicePkg}/bin/hostenv-provider-service
      '';
    in
    {
      config = lib.mkIf providerEnabled {
        packages.hostenv-provider-service = servicePkg;
        apps.hostenv-provider-service = {
          type = "app";
          program = "${servicePkg}/bin/hostenv-provider-service";
          meta.description = "Hostenv provider service (webhooks + admin UI) app";
        };
        hostenv.haskell.devPackages = packageNames;
        hostenv.devShell.packages = [ servicePkg servicePkgDev ];
      };
    });
}
