{ inputs, lib, ... }:
let
  fp = inputs.flake-parts.lib;
in
{
  options.perSystem = fp.mkPerSystemOption ({ config, pkgs, self', ... }:
    let
      ghcPackageNames = lib.unique config.hostenv.haskell.devPackages;
      devGhc = pkgs.haskellPackages.ghcWithPackages (p: map (name: p.${name}) ghcPackageNames);
    in
    {
      options.hostenv.devShell = {
        packages = lib.mkOption { type = lib.types.listOf lib.types.package; default = [ ]; };
        inputsFrom = lib.mkOption { type = lib.types.listOf lib.types.package; default = [ ]; };
        shellHook = lib.mkOption { type = lib.types.lines; default = ""; };
      };
      options.hostenv.haskell.devPackages = lib.mkOption {
        type = lib.types.listOf lib.types.str;
        default = [ ];
        description = "Haskell package names contributed by micro-projects for the default dev shell GHC environment.";
      };

      config = {
        hostenv.haskell.devPackages =
          [ "haskell-language-server" ]
          ++ (lib.attrByPath [ "provider" "haskellDevPackages" ] [ ] config);
        devShells.default = pkgs.mkShell {
          inputsFrom = config.hostenv.devShell.inputsFrom;

          buildInputs =
            [
              config.packages.hostenv-provider
              config.packages.hostenv-provider-plan
              pkgs.sops
              pkgs.age
              pkgs.jq
              pkgs.bind
              pkgs.deploy-rs
              pkgs.git
              pkgs.postgresql
              pkgs.socat
              pkgs.haskellPackages.cabal-install
              devGhc
            ]
            ++ config.hostenv.devShell.packages;

          shellHook = ''
            if command -v ghc >/dev/null; then
              libdir="$(ghc --print-libdir)"
              pkgdb="''${libdir}/package.conf.d"
              cat > "$PWD/cabal.project.local" <<EOF
-- Generated by nix develop (flake-parts/devshells.nix)
package-dbs: clear, ''${pkgdb}
with-compiler: ghc
EOF
            fi
          '' + "\n" + config.hostenv.devShell.shellHook;
        };
      };
    });
}
